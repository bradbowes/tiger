use "sources.tig"
use "symbols.tig"

and_token = 1
array_token = 2
assign_token = 3
begin_token = 4
char_token = 5
colon_token = 6
comma_token = 7
comment_token = 8
div_token = 9
do_token = 10
dot_token = 11
else_token = 12
end_token = 13
eof_token = 14
eq_token = 15
false_token = 16
for_token = 17
geq_token = 18
gt_token = 19
id_token = 20
if_token = 21
in_token = 22
lbrace_token = 23
lbracket_token = 24
leq_token = 25
let_token = 26
lparen_token = 27
lt_token = 28
minus_token = 29
mod_token = 30
mul_token = 31
neq_token = 32
nil_token = 33
number_token = 34
of_token = 35
or_token = 36
plus_token = 37
rbrace_token = 38
rbracket_token = 39
rparen_token = 40
semicolon_token = 41
string_token = 42
then_token = 43
to_token = 44
true_token = 45
type_token = 46
use_token = 47
while_token = 48

type token = {
   tag: int,
   value: symbol,
   file_name: string,
   line: int,
   col: int
}

tok = token { tag = 0, value = nil, file_name = "", line = 0, col = 0 }
buf = make_buffer(4096)

and_kw = intern_string("and")
array_kw = intern_string("array")
begin_kw = intern_string("begin")
do_kw = intern_string("do")
else_kw = intern_string("else")
end_kw = intern_string("end")
false_kw = intern_string("false")
for_kw = intern_string("for")
if_kw = intern_string("if")
in_kw = intern_string("in")
let_kw = intern_string("let")
mod_kw = intern_string("mod")
nil_kw = intern_string("nil")
of_kw = intern_string("of")
or_kw = intern_string("or")
then_kw = intern_string("then")
true_kw = intern_string("true")
type_kw = intern_string("type")
use_kw = intern_string("use")
while_kw = intern_string("while")


push_buf() =
   begin
      push_buffer(buf, ch)
      nextch()
   end

tok_location(): location =
   location {
      file_name = tok.file_name,
      line = tok.line,
      col = tok.col
   }

skip_spaces() =
   while is_space(ch) do
      nextch();

recognize(tag: int) =
   begin
      tok.tag := tag
      tok.value := intern(buf)
   end

push_tag(tag: int) =
   begin
      push_buf()
      recognize(tag)
   end

scan_comment() =
   begin
      push_buf()
      while buf.ptr < 4 or ch <> #"/" do
         begin
            while ch <> #"*" do
               push_buf()
            while ch = #"*" do
               push_buf()
         end
      push_buf()
      recognize(comment_token)
   end

scan_string() =
   let
      escape = chr(0)
      code = 0
   in
      nextch()
      while ch <> #"\"" do
         if ch = #"\\" then
            let
               esc = true
            in
               nextch();
               if ch = #"t" then escape := #"\t"       /* tab */
               else if ch = #"n" then escape := #"\n"  /* newline */
               else if ch = #"v" then escape := #"\v"  /* vertical tab */
               else if ch = #"f" then escape := #"\f"  /* form feed */
               else if ch = #"r" then escape := #"\r"  /* carriage return */
               else if ch = #"\\" or ch = #"\"" or ch = #"\'" then escape := ch
               else if ch = #"^" then
                  begin
                     nextch()
                     if is_upper(ch) then
                        escape := chr(ord(ch) - 64)
                     else if is_lower(ch) then
                        escape := chr(ord(ch) - 96)
                     else
                        err("illegal escape sequence", src_location())
                  end
               else if is_digit(ch) then
                  begin
                     code := ord(ch) - ord(#"0")
                     nextch()
                     if is_digit(ch) then
                        code := code * 10 + ord(ch) - ord(#"0")
                     else
                        err("illegal escape sequence", src_location())
                     nextch();
                     if is_digit(ch) then
                        code := code * 10 + ord(ch) - ord(#"0")
                     else
                        err("illegal escape sequence", src_location())
                     if code > 255 then
                        err("illegal escape sequence", src_location())
                     escape := chr(code);
                  end
               else if is_space(ch) then
                  begin
                     skip_spaces()
                     if ch = #"\\" then
                        nextch()
                     else
                        err("illegal escape sequence", src_location())
                     esc := false
                  end
               else
                  err("illegal escape sequence", src_location())
               if esc then
                  begin
                     push_buffer(buf, escape)
                     nextch()
                  end
            end
         else
            push_buf()
      recognize(string_token)
      nextch()
   end

scan_char() =
   begin
      nextch()
      if ch = #"\"" then
         scan_string()
      else
         err("illegal character literal", src_location())
      if length(tok.value.id) <> 1 then
         err("illegal character literal", tok_location())
      recognize(char_token)
   end;

scan_number() =
   begin
      while is_digit(ch) do
         push_buf();
      recognize(number_token)
   end;

scan_id() =
   begin
      while is_alpha(ch) or is_digit(ch) or ch = #"_" do
         push_buf();
      recognize(id_token)
      let
         value = tok.value
      in
         tok.tag := if value = and_kw then and_token
            else if value = array_kw then array_token
            else if value = begin_kw then begin_token
            else if value = do_kw then do_token
            else if value = else_kw then else_token
            else if value = end_kw then end_token
            else if value = false_kw then false_token
            else if value = for_kw then for_token
            else if value = if_kw then if_token
            else if value = in_kw then in_token
            else if value = let_kw then let_token
            else if value = mod_kw then mod_token
            else if value = nil_kw then nil_token
            else if value = of_kw then of_token
            else if value = or_kw then or_token
            else if value = then_kw then then_token
            else if value = true_kw then true_token
            else if value = type_kw then type_token
            else if value = use_kw then use_token
            else if value = while_kw then while_token
            else id_token
      end
   end

scan() =
   begin
      skip_spaces()
      clear_buffer(buf)
      tok.value := nil
      tok.file_name := src.file_name
      tok.line := src.line
      tok.col := src.col
      if ch = EOF then recognize(eof_token)
      else if ch = #"," then push_tag(comma_token)
      else if ch = #";" then push_tag(semicolon_token)
      else if ch = #"." then push_tag(dot_token)
      else if ch = #"(" then push_tag(lparen_token)
      else if ch = #")" then push_tag(rparen_token)
      else if ch = #"[" then push_tag(lbracket_token)
      else if ch = #"]" then push_tag(rbracket_token)
      else if ch = #"{" then push_tag(lbrace_token)
      else if ch = #"}" then push_tag(rbrace_token)
      else if ch = #"+" then push_tag(plus_token)
      else if ch = #"-" then push_tag(minus_token)
      else if ch = #"*" then push_tag(mul_token)
      else if ch = #"/" then
         begin
            push_buf();
            if ch = #"*" then scan_comment()
            else recognize(div_token)
         end
      else if ch = #"=" then push_tag(eq_token)
      else if ch = #"<" then
         begin
            push_buf()
            if ch = #">" then push_tag(neq_token)
            else if ch = #"=" then push_tag(leq_token)
            else recognize(lt_token)
         end
      else if ch = #">" then
         begin
            push_buf()
            if ch = #"=" then push_tag(geq_token)
            else recognize(gt_token)
         end
      else if ch = #":" then
         begin
            push_buf()
            if ch = #"=" then push_tag(assign_token)
            else recognize(colon_token)
         end
      else if is_digit(ch) then scan_number()
      else if ch = #"\"" then scan_string()
      else if ch = #"#" then scan_char()
      else if is_alpha(ch) then  scan_id()
      else err("Illegal character", src_location())
   end
